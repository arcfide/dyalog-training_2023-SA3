:Namespace MG

⍝ https://www.nas.nasa.gov/assets/nas/pdf/techreports/1994/rnr-94-007.pdf

⎕IO←0

DS←256 ⋄ IT←4 ⋄ k←2⍟DS

CI←+⌿¨|RI←¯1+⍳3 3 3
A←(¯8÷3)  (0)  (÷6)(÷12)[CI]
P←(÷2     4    8     16)[CI]
Q←(÷1     2    4      8)[CI]
Sa←(¯3÷8) (÷32)(÷¯64) 0 [CI]
Sb←(¯3÷17)(÷33)(÷¯61) 0 [CI]
S←Sa

pp←{⍺←'' ⋄ ⍵⊣⎕←⍺(L2 ⍵)}
st←{k←⍺
	z←⊃{(¯1↑[⍺]⍵),[⍺]⍵,[⍺]1↑[⍺]⍵}⌿(⍳≢⍴⍵),⊂⍵
	z←{+⌿,k×⍵}⌺3 3 3⊢z
	¯1 ¯1 ¯1↓1 1 1↓z
}
dn←{⍵[1+2×⍳(≢⍴⍵)⍴(≢⍵)÷2]}
up←{X⊣X[1+2×⍳(≢⍴⍵)⍴≢⍵]←⍵⊣X←0⍴⍨2×⍴⍵}

M←{⍺≤1:S st ⍵ ⋄ z+S st ⍵-A st⊢z←Q st up(⍺-1)∇dn P st ⍵}
L2←{((+⌿,⍵×⍵)÷×⌿⍴⍵)*.5}
MG←{L2 ⍺-A st ⍺{⍵+k M ⍺-A st ⍵}⍣IT⊢⍵}

seed←314159265
rlcg←{⎕FR←1287 ⋄ a←5*13 ⋄ b←⍺ ⋄ m←2*46 ⋄ z←(2*¯46)×{b∘←m|a×b}¨⍳⍵ ⋄ ⎕FR←645 ⋄ 1×z}

Run←{
	⎕←'Initializing data...'
	v←{(10⌿¯1 1)@(⊂[0](3⍴256)⊤(10↑r),¯10↑r←⍋seed rlcg DS*3)⊢(3⍴DS)⍴0}⍬
	u←DS DS DS⍴0
	
	⎕←'Warming up...'
	r←u+k M v-A st u
	
	⎕←'Running MG...'
	start←24 60 60 1000⊥¯4↑⎕TS
	r←v MG u
	end←24 60 60 1000⊥¯4↑⎕TS
	
	⎕←'MG time elapsed: ',(⍕(end-start)÷1000),'secs'
	⎕←'L2 Norm: ',⍕r
	1:shy←r
}

:EndNamespace
